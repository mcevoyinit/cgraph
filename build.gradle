buildscript {
    ext {
        kotlin_group = 'org.jetbrains.kotlin'
        kotlin_version = '1.2.71'

        corda_artifactory_url = 'https://software.r3.com/artifactory'
        corda_group = 'net.corda'
        corda_release_version = '4.8'

        corda_gradle_plugin_group = 'net.corda.plugins'
        corda_gradle_plugin_version = '5.0.4'

        junit_group = 'org.junit.jupiter'
        junit_version = '5.3.1'

        cordapp_platform_version = 10
        cordapp_contract_name = 'CGraph Core Contract'
        cordapp_workflow_name = 'CGraph Core Workflow'
        cordapp_vendor_name = 'CGraph'
        cordapp_license = 'Apache License, Version 2.0'
        cordapp_version_id = 1
        cordapp_signing_enabled = false
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-dependencies" }
    }

    dependencies {
        classpath "$kotlin_group:kotlin-gradle-plugin:$kotlin_version"
        classpath "$corda_gradle_plugin_group:cordapp:$corda_gradle_plugin_version"
    }
}

group 'io.cgraph'
version '1.0.0'

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-dependencies" }
        maven { url "https://repo.gradle.org/gradle/libs-releases" }
    }

    configurations {
        all {
            exclude module: 'slf4j-log4j12'
            exclude module: 'log4j-slf4j-impl'
        }
    }

    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'
    apply plugin: 'net.corda.plugins.cordapp'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    jar {
        exclude '**/log4j2*.xml'
    }

    test {
        jvmArgs = ["-ea", "-javaagent:../lib/quasar.jar"]
        maxHeapSize = "4096m"
        useJUnitPlatform()
    }
}
