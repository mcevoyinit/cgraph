buildscript {
    ext {
        kotlin_group = 'org.jetbrains.kotlin'
        kotlin_version = '1.2.71'

        corda_artifactory_url = 'https://software.r3.com/artifactory'
        corda_group = 'net.corda'
        corda_release_version = '4.8'

        corda_gradle_plugin_group = 'net.corda.plugins'
        corda_gradle_plugin_version = '5.0.13'

        junit_group = 'org.junit.jupiter'
        junit_version = '5.3.1'

        log4j_group = "org.apache.logging.log4j"
        log4j_version = "2.11.2"

        cordapp_platform_version = 10
        cordapp_license = 'Apache License, Version 2.0'
        cordapp_version_id = 1
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-dependencies" }
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "$kotlin_group:kotlin-gradle-plugin:$kotlin_version"
        classpath "$corda_gradle_plugin_group:cordapp:$corda_gradle_plugin_version"
    }
}

group 'com.cgraph'
version '1.0.0'

subprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "$corda_artifactory_url/corda-releases" }
        maven { url "$corda_artifactory_url/corda-dependencies" }
        maven { url "https://repo.gradle.org/gradle/libs-releases" }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/mcevoyinit/cgraph")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    configurations {
        all {
            exclude module: 'slf4j-log4j12'
            exclude module: 'log4j-slf4j-impl'
        }
    }

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'net.corda.plugins.cordapp'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            languageVersion = "1.2"
            apiVersion = "1.2"
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    jar {
        exclude '**/log4j2*.xml'
    }

    test {
        jvmArgs = ["-ea", "-javaagent:../lib/quasar.jar"]
        maxHeapSize = "8192m"
        useJUnitPlatform()
    }
}

task deployCGraph(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    nodeDefaults {
        projectCordapp {
            deploy = false
        }
        rpcUsers = [[user: "guest", "password": "letmein", "permissions": ["ALL"]]]
    }
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating: false]
        p2pPort 10002
        rpcSettings {
            address("localhost:10003")
            adminAddress("localhost:10043")
        }
        cordapps.clear()
    }

    node {
        name "O=Lender,L=London,C=GB"
        cordapp(project(':cgraph-core')) {
            config '''
                graphQLUrl="<INSERT_GRAPHQL_URL"
                graphQLToken=<INSERT_GRAPHQL_TOKEN"
                graphBraidServerPort=8080
             '''
        }
        cordapp(project("cgraph-example")) {
            config '''
                braidServerPort=9090
             '''
        }
        p2pPort 10004
        rpcSettings {
            address("localhost:10005")
            adminAddress("localhost:10045")
        }
        // Starts an internal SSH server providing a management shell on the node.
        sshdPort 2222
    }
    node {
        name "O=Borrower,L=Berlin,C=DE"
        cordapp(project(':cgraph-core')) {
            config '''
                graphQLUrl="<INSERT_GRAPHQL_URL"
                graphQLToken=<INSERT_GRAPHQL_TOKEN"
                graphBraidServerPort=8081
             '''
        }
        cordapp(project("cgraph-example")) {
            config '''
                braidServerPort=9091
             '''
        }
        p2pPort 10008
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10049")
        }
        // Starts an internal SSH server providing a management shell on the node.
        sshdPort 2223
    }
}